buildscript {

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    //https://mvnrepository.com/artifact/org.jetbrains.intellij/org.jetbrains.intellij.gradle.plugin
    id "org.jetbrains.intellij" version "0.4.11"
}

group 'com.itangcent'
version plugin_version

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'idea'
sourceCompatibility = 1.8

intellij {
    version idea_version
    pluginName plugin_name
    updateSinceUntilBuild false
    downloadSources true
    sandboxDirectory "idea-sandbox"
    instrumentCode = true
    updateSinceUntilBuild false
    sameSinceUntilBuild false

    plugins "org.jetbrains.kotlin:1.3.11-release-IJ2017.3-1"
    //plugins "org.jetbrains.kotlin:1.3.11-release-IJ2017.3-1", "org.intellij.scala:2017.3.11"
    //plugins 'org.jetbrains.kotlin:1.3.21-release-IJ2018.2-1' //dependency kotlin


//    plugins = ['java']
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

patchPluginXml {
    pluginDescription(file(descriptionFile).text)
    changeNotes(file(changesFile).text)
}

jar {
    excludes = ["com/google/common/**"
                , "org/checkerframework/checker/**"
                , "afu/org/checkerframework/checker/**"
                , "afu/org/plume/**"
                , "kotlin/**"
                , "org/jetbrains/annotations/**"
                , "org/intellij/langannotations/**"
    ]
//    excludes = ["com/google/common/**"]
//    excludes = ["com/google/common/**", "org/apache/http/util/**"]
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
//    from { configurations.runtime }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

repositories {

//    flatDir {
//        dirs 'libs'
//    }

    mavenCentral()

}

dependencies {
    implementation(project(path: ':common-api', configuration: 'default')) {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

//    implementation fileTree(dir: 'libs', include: ['*.jar'])


    implementation('com.itangcent:intellij-idea:0.9.3-SNAPSHOT') {
        exclude group: 'com.google.inject'
        exclude group: 'com.google.code.gson'
    }

    implementation('com.itangcent:intellij-kotlin-support:0.9.3-SNAPSHOT') {
        exclude group: 'com.google.inject'
        exclude group: 'com.google.code.gson'
    }

//    implementation('com.itangcent:intellij-scala-support:0.9.3-SNAPSHOT') {
//        exclude group: 'com.google.inject'
//        exclude group: 'com.google.code.gson'
//    }

    implementation("com.google.inject:guice:4.2.2") {
        exclude group: 'org.checkerframework', module: 'checker-compat-qual'
        exclude group: 'com.google.guava', module: 'guava'
    }

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // https://mvnrepository.com/artifact/org.jsoup/jsoup
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.12.1'

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junit_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit_version}"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlin_version"

    // https://mvnrepository.com/artifact/com.nhaarman.mockitokotlin2/mockito-kotlin
    testImplementation group: 'com.nhaarman.mockitokotlin2', name: 'mockito-kotlin', version: '2.2.0'

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


task findSpi {
    doLast {
        try {
            def names = [] as Set<String>
            def duplicated = [] as Set<String>
            //find duplicated files
            configurations.compile.forEach {
                if (it.isFile()) {
                    zipTree(it)
                            .filter { it.parentFile.path.endsWith("META-INF/services") }
                            .forEach {
                                if (!names.add(it.name)) {
                                    if (!duplicated.contains(it.name)) {
                                        duplicated.add(it.name)
                                    }
                                }
                            }
                }
            }

            def servicesDir = new File("./src/main/resources/META-INF/services/")
            servicesDir.delete()
            servicesDir.mkdirs()

            duplicated.forEach {
                try {
                    def targetFile = new File(servicesDir, it)
                    targetFile.createNewFile()
                    targetFile.text = ""
                } catch (e) {
                    println("error:" + e)
                }
            }

            configurations.compile.forEach {
                if (it.isFile()) {
                    zipTree(it)
                            .filter {
                                it.parentFile.path.endsWith("META-INF/services") &&
                                        duplicated.contains(it.name)
                            }
                            .forEach {
                                try {
                                    def targetFile = new File(servicesDir, it.name)
                                    if (!targetFile.exists()) {
                                        targetFile.createNewFile()
                                    }
                                    targetFile.append(it.text)
                                    targetFile.append("\n")
                                } catch (e) {
                                    println("error to append services info to:" + it.name + e)
                                }
                            }
                }
            }
        } catch (e) {
            println("error to find spi:" + e)
        }
    }
}

test {
    useJUnitPlatform()
}